buildscript {
    repositories {
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
		maven {
			url 'https://plugins.gradle.org/m2/'
		}
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
		classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
        
    }
}

plugins {
	  id 'eclipse'
	  id 'idea'
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.matthewprenger.cursegradle'

version = "${minecraft_version}-1.0.0"
group= "io.github.nagol2003" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "Additional Planets"

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
    version = "${minecraft_version}-${forge_version}"
    runDir = "run"
    mappings = "${mappings_version}"
    makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

idea.module {
    inheritOutputDirs=true
    downloadJavadoc = true
    iml {
        whenMerged { module ->
            module.dependencies*.exported = true
        }
    }
}

apply from: "https://raw.githubusercontent.com/ReadOnlyDevelopment/gradle/galacticraft/dev-latest.gradle"

repositories {
    removeIf { it.url.host == 'files.minecraftforge.net' }
    maven {
        url "https://maven.minecraftforge.net/"
    }
        maven {
        url "https://www.cursemaven.com"
    }
    maven {
        name 'prog'
        url 'https://dvs1.progwml6.com/files/maven'
    }
    maven {
        name 'curseforge'
        url 'https://minecraft.curseforge.com/api/maven/'
    }
    maven {
        name 'BlameJared - Crafttweaker'
        url 'https://maven.blamejared.com/'
    }
}

dependencies {
    compile "mezz.jei:jei_${minecraft_version}:${jei_version}:api"
    runtime "mezz.jei:jei_${minecraft_version}:${jei_version}"
    
    compile("CraftTweaker2:CraftTweaker2-API:${crafttweaker_version}") {
        exclude module: "asm-debug-all"
    }
    compile("CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-${crafttweaker_version}") {
        exclude module: "asm-debug-all"
    }
	deobfCompile "curse.maven:asmodeuscore-0.0.25-303101:3231302"
}

processResources {

    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

task apiJar(type: Jar) {
    from(sourceSets.main.output) {
        include "net/rom/addontutorial/api/**"
    }
	from(sourceSets.main.java) {
        include "net/rom/addontutorial/api/**"
    }
    classifier = 'api'
}

task signJar(type: SignJar, dependsOn: reobfJar) {
    onlyIf {
        project.hasProperty('keyStore')
    }
    keyStore = project.findProperty('keyStore')
    alias = project.findProperty('keyStoreAlias')
    storePass = project.findProperty('keyStorePass')
    keyPass = project.findProperty('keyStoreKeyPass')
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

curseforge {
    if (project.hasProperty('curseforge_key')){
		apiKey = project.findProperty("curseforge_key")
		project {
			id = "PROJECT-ID"
			releaseType = "release"
			addGameVersion "${minecraft_version}"
			mainArtifact(jar)
			addArtifact(apiJar)
			addArtifact(deobfJar)
		}
	}
}

build.dependsOn signJar
tasks.build.dependsOn signJar
tasks.curseforge.dependsOn build