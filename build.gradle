plugins {
	id "net.galacticraft.addon" version "1.0.0"
	id 'io.freefair.lombok' version '6.5.0.2'
	id 'eclipse'
}

version = "${minecraft_version}-1.0.0"
group = "io.github.nagol2003"
archivesBaseName = "AdditionalPlanets"

galacticraft {
	version("4.0.3")
}

minecraft {

    mappings channel: mappings_channel, version: mappings_version

    runs {
        client {
            workingDirectory project.file('run/client')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
			if (project.hasProperty('uuid'))
				args '--uuid', project.getProperty('uuid')
			if (project.hasProperty('username'))
				args '--username', project.getProperty('username')
        }

        server {
            workingDirectory project.file('run/server')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    mavenCentral()
	maven { url "https://www.cursemaven.com" }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
	implementation "curse.maven:asmodeuscore-303101:3231302"
}

apply from: 'gradle/resources.gradle'

jar.finalizedBy('reobfJar')

task deobfJar(type: Jar) {
    duplicatesStrategy(DuplicatesStrategy.FAIL)
    archiveClassifier.set('dev')
	from sourceSets.main.output
}

clean {
	def filteredDelete = new HashSet<>()
	for (def toDelete : getDelete()) {
		for (def f : file(toDelete).listFiles()) {
			if (f.getName() != "fg_cache") {
				filteredDelete.add(f)
			}
		}
	}
	setDelete(filteredDelete)
}
