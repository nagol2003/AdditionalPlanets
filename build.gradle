buildscript {
    repositories {
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
		maven {
			url 'https://plugins.gradle.org/m2/'
		}
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
		classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
        
    }
}

plugins {
	  id 'eclipse'
	  id 'idea'
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.matthewprenger.cursegradle'

version = "${minecraft_version}-1.0.0"
group= "io.github.nagol2003" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "AdditionalPlanets"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
    version = "${minecraft_version}-${forge_version}"
    runDir = "run"
    mappings = "${mappings_version}" 
    makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

idea.module {
    inheritOutputDirs=true
    downloadJavadoc = true
    iml {
        whenMerged { module ->
            module.dependencies*.exported = true
        }
    }
}

String GCBuild = "261"
String GCVersion = "1.12.2-4.0.2.${GCBuild}"

repositories {
	mavenCentral()
	ivy {
		url "https://micdoodle8.com/new-builds/"
		layout 'pattern', {
			artifact "[organization]/${GCBuild}/[module]-[revision].jar"
		}
		metadataSources {
			artifact()
		}
	}
    maven { url "https://www.cursemaven.com" }
}

dependencies {

	compile "GC-1.12-dev:GalacticraftCore-Dev:${GCVersion}"
	compile "GC-1.12-dev:Galacticraft-Planets-Dev:${GCVersion}"
	compile "GC-1.12-dev:MicdoodleCore-Dev:${GCVersion}"
	compile "curse.maven:asmodeuscore-303101:3231302"
	
	compileOnly "org.projectlombok:lombok:${lombok_version}"
	annotationProcessor "org.projectlombok:lombok:${lombok_version}"

	testCompileOnly "org.projectlombok:lombok:${lombok_version}"
	testAnnotationProcessor "org.projectlombok:lombok:${lombok_version}"
	
}

processResources {

    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
		expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

task signJar(type: SignJar, dependsOn: reobfJar) {
    onlyIf {
        project.hasProperty('keyStore')
    }
    keyStore = project.findProperty('keyStore')
    alias = project.findProperty('keyStoreAlias')
    storePass = project.findProperty('keyStorePass')
    keyPass = project.findProperty('keyStoreKeyPass')
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

curseforge {
    if (project.hasProperty('curseforge_key')){
		apiKey = project.findProperty("curseforge_key")
		project {
			id = "PROJECT-ID"
			releaseType = "release"
			addGameVersion "${mcVersion}"
			mainArtifact(jar) {
				//relations {
				//	requiredLibrary 'some-coremod'
				//}
			}
			addArtifact(apiJar)
			addArtifact(deobfJar)
		}
	}
}

build.dependsOn signJar
tasks.build.dependsOn signJar
tasks.curseforge.dependsOn build